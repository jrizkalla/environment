source $HOME/.bash_profile

fpath=($ENV/zsh/plugins/complete_funcs $fpath)

# Load colors
autoload -U colors && colors

# Customize the prompt
setopt prompt_subst
setopt extendedglob
setopt hist_ignorespace

alias kk=' echo "$(fc -ln -1)" && eval "$(fc -ln -1)"'

BASE_PROMPT1="%{$fg[yellow]%}%!. [%{$fg[cyan]%}%D{%I:%M:%S %p}%{$fg[yellow]%}]" # 1. [time]
BASE_PROMPT2="$> %{$reset_color%}" #$> 
# Set a timeout to 1 second (to update the clock)
# 
#TMOUT=1
#function TRAPALRM {
#    if [ "$WIDGET" != "expand-or-complete" ]; then
#        zle reset-prompt
#    fi
#}

BASE_RPROMPT="%{$fg[white]%}%1m %{$fg[cyan]%}%1d%{$reset_color%}"
RPROMPT="$BASE_RPROMPT"

#PS2='%_ > '


# Enable auto completion
zmodload zsh/complist
autoload -U compinit && compinit
zstyle ':completion:*' menu select
zstyle ':completion:*' list-colors "${(@s.:.)LS_COLORS}"
setopt completealiases
setopt menu_complete

# Type the name of the directory without cd
setopt autocd
# don't enable correction (I don't like it)
# setopt correctall

# Set vi mode
bindkey -v
#export KEYTIMEOUT=20 # Kill the lag (don't need this with jk instead of <esc>)
bindkey -M viins 'jk' vi-cmd-mode
bindkey '^?' backward-delete-char
function ctrl-z-widget {
    clear
    fg
}
zle -N ctrl-z-widget
bindkey '^z' ctrl-z-widget


# Add an indicator of the current mode (normal or insert)
function zle-keymap-select {
    mode="n"
    if [ "$KEYMAP" = "main" ]; then
        mode="i"
    fi
    PROMPT="$BASE_PROMPT1 $mode$BASE_PROMPT2"
    zle reset-prompt
}
zle -N zle-keymap-select

# Git branch in status
function zle-line-init {
    zle && zle zle-keymap-select
    branch=$(basename "$(git symbolic-ref HEAD 2>/dev/null)")
    # Set the color based on wether the current repo is clean or not
    git diff --cached --exit-code >/dev/null 2>&1
    if [ $? -eq 0 ]; then
        # are there untracked files?
        if [ -z "$(git status --porcelain)" ]; then
            color='green'
        else
            color='red'
        fi
    else
        color='red'
    fi
    
    if [ -n "$branch" ]; then
        branch="[%{$fg[$color]%}$branch%{$reset_color%}]"
    fi
    
    # also get the Python virtual environment name (if it exists)
    if [ -n "$VIRTUAL_ENV" ]; then
        virtual_env="(%{$fg[yellow]%}$(basename $VIRTUAL_ENV)%{$reset_color%})"
    else
        virtual_env=""
    fi
    
    RPROMPT="$BASE_RPROMPT$virtual_env $branch"
    zle && zle reset-prompt
}
zle -N zle-line-init

# Save the history between sessions
HISTSIZE=1000
HISTFILE="$HOME/.zsh_history_file"
SAVEHIST=1000

# Share the history
setopt SHARE_HISTORY

# Enable fish like syntax highlighting
source "$ENV/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
# Enable fish like autosuggestions
source "$ENV/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh"

# Bind Ctrl+k to complete a suggestiong (as in ok)
bindkey '^k' autosuggest-accept
bindkey '^j' forward-word

# Add RVM to PATH for scripting. Make sure this is the last PATH variable change.
export PATH="$PATH:$HOME/.rvm/bin"


[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
